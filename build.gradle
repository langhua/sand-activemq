/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

def activemqVersion = '5.16.7',
    springVersion = '4.3.30.RELEASE'

configurations.all {
    exclude group: 'org.eclipse.jetty'
    exclude group: 'org.eclipse.jetty.websocket'
    exclude group: 'org.springframework.boot', module: 'spring-boot-devtools'
}

applicationDefaultJvmArgs.add('--add-opens=java.base/java.io=ALL-UNNAMED')
applicationDefaultJvmArgs.add('--add-opens=java.rmi/sun.rmi.transport=ALL-UNNAMED')
applicationDefaultJvmArgs.add('--add-opens=java.base/java.lang=ALL-UNNAMED')
applicationDefaultJvmArgs.add('-Dactivemq.conf=plugins/sand-activemq/config')

def logArgFound = false
def originalLog4jConfig = ""
def log4jConfig = ""

applicationDefaultJvmArgs.each { jvmArg ->
    if (jvmArg && jvmArg.startsWith("-Dlog4j.configurationFile=") && !logArgFound) {
        originalLog4jConfig = jvmArg
        if (!jvmArg.endsWith("=")) {
            jvmArg += ","
        }
        log4jConfig = jvmArg + "log4j2-activemq.xml"
        logArgFound = true
    }
}

if (!logArgFound) {
    applicationDefaultJvmArgs.add('-Dlog4j.configurationFile=log4j2.xml,log4j2-activemq.xml')
} else {
    applicationDefaultJvmArgs.remove(originalLog4jConfig)
    applicationDefaultJvmArgs.add(log4jConfig)
}

dependencies {
    pluginLibsCompile 'org.apache.activemq:activemq-broker:' + activemqVersion,
            'org.apache.activemq:activemq-jaas:' + activemqVersion,
            'org.apache.activemq:activemq-web:' + activemqVersion,
            'org.apache.activemq:activemq-console:' + activemqVersion,
            'org.osgi:osgi.core:6.0.0',
            'org.osgi:osgi.cmpn:6.0.0',
            'org.apache.tomcat:tomcat-util:9.0.83'

    pluginLibsCompile ('org.springframework:spring-test:' + springVersion) {
        force true
    }

    pluginLibsRuntime ('org.slf4j:slf4j-api:1.7.36') {
        force true
    }

    pluginLibsRuntime 'com.lmax:disruptor:3.4.2',
            'org.apache.activemq:activemq-spring:' + activemqVersion,
            'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:1.2.5',
            'org.apache.taglibs:taglibs-standard-impl:1.2.5'
}
